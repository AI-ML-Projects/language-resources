# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Sinhala Verbalizer for decimals. Can verbalize a decimal thats contain both
# an integer part and a fractional part. An exponent is also support here.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'cardinal.grm' as c;
import 'digit.grm' as d;

minus_word = Project[c.MINUS_NAME, 'output'];

# Handle the 'negative: {true,false}' part.
negative =
  (universal_rules.negative_true : minus_word " ") | (universal_rules.negative_false : "")
;

# Integer part and all the digits in the fractional part except the last digit,
# are modified with a Sinhala equivalent of 'and'.
# i.e 3.456 is read as the equivalent of "threeයි point fourයි fiveයි six".
sinhala_decimal_and = "" : "යි";
integer_fraction =
  negative?
  universal_rules.integer_part
  c.CARDINAL
  universal_rules.rec_sep
  (("" : sinhala_decimal_and universal_rules.ins_space "දශම" universal_rules.ins_space)
   universal_rules.fractional_part
   (d.DIGIT
    (sinhala_decimal_and (universal_rules.ins_space d.DIGIT))*)
   universal_rules.rec_sep)?
;

# Handle the 'exponent: "<cardinal>"' part.
exponent =
  universal_rules.exponent
  ("" : universal_rules.ins_space "බල" universal_rules.ins_space)
  c.CARDINAL
  universal_rules.rec_sep
;

export DECIMAL = Optimize[integer_fraction exponent?];

export DECIMAL_MARKUP = Optimize[
  universal_rules.semclass_decimal
  DECIMAL
];
