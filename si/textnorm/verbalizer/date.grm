# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Sinhala Verbalizer for dates. Takes ascii protocol buffers for a
# date and converts it to regular Sinhala words.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'cardinal.grm' as c;

month_number_to_full_name =
    ("1" : "ජනවාරි")
  | ("2" : "පෙබරවාරි")
  | ("3" : "මාර්තු")
  | ("4" : "අප්‍රේල්")
  | ("5" : "මැයි")
  | ("6" : "ජුනි")
  | ("7" : "ජූලි")
  | ("8" : "අගෝස්තු")
  | ("9" : "සැප්තැම්බර්")
  | ("10" : "ඔක්තෝබර්")
  | ("11" : "නොවැම්බර්")
  | ("12" : "දෙසැම්බර්")
;

month_full_name =
    "ජනවාරි"
  | "පෙබරවාරි"
  | "මාර්තු"
  | "අප්‍රේල්"
  | "මැයි"
  | "ජුනි"
  | "ජූලි"
  | "අගෝස්තු"
  | "සැප්තැම්බර්"
  | "ඔක්තෝබර්"
  | "නොවැම්බර්"
  | "දෙසැම්බර්"
;

months = month_number_to_full_name | month_full_name;

era_names = Optimize[
  ("BC" : "ක්‍රිස්තු පූර්ව") |
  ("AD" : "ක්‍රිස්තු වර්ෂ") |
  ("BE" : "බුද්ධ වර්ෂ")    # Buddhist Era.
];

# Year will be modified by adding the diacritic, SINHALA_VOWEL_SIGN_DIGA_KOMBUVA
# (U+0DDA) at the end of the cardinal number name of year.
insert_sinhala_diga_kombuva = ("" : "[0xE0][0xB7][0x9A]");

year_part = universal_rules.year c.CARDINAL insert_sinhala_diga_kombuva universal_rules.rec_sep;
month_part = universal_rules.month (universal_rules.delete_initial_zero @ months) universal_rules.rec_sep;
date_part = universal_rules.day c.CARDINAL universal_rules.rec_sep;
era_part = universal_rules.era era_names universal_rules.rec_sep;

yyyy_mm_dd_style = Optimize[
  (era_part universal_rules.ins_space)?
  year_part
  universal_rules.ins_space
  month_part
  universal_rules.ins_space
  date_part
  universal_rules.style2
];

yyyy_mm_style = Optimize[
  (era_part universal_rules.ins_space)?
  year_part
  universal_rules.ins_space
  month_part
  universal_rules.style2
];

mm_dd_style = Optimize[
  month_part
  universal_rules.ins_space
  date_part
  universal_rules.style1
];

mm_dd_yyyy_style = Optimize[
  month_part
  universal_rules.ins_space
  date_part
  universal_rules.ins_space
  year_part
  universal_rules.style1
];

era_yyyy_style = Optimize[
  era_part
  universal_rules.ins_space
  year_part
  universal_rules.style2
];

export DATE_MARKUP = Optimize[
  universal_rules.semclass_date
  (yyyy_mm_dd_style |
   yyyy_mm_style |
   mm_dd_style |
   mm_dd_yyyy_style |
   era_yyyy_style)
];
