# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Parser for tokens connecting other tokens.

import 'bangla.grm' as b;
import 'date.grm' as date;
import 'measure.grm' as measure;
import 'telephone.grm' as telephone;
import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;

weekdays = ("mon" : "monday") |
           ("tue" : "tuesday") |
           ("tues" : "tuesday") |
           ("wed" : "wednesday") |
           ("weds" : "wednesday") |
           ("thur" : "thursday") |
           ("thurs" : "thursday") |
           ("fri" : "friday") |
           ("sat" : "saturday") |
           ("sun" : "sunday");

range_connector = universal_func.connectorMarkup[(("-" | "—" | "–") : "range")];

weekday_range = universal_func.basic_connector[
  range_connector,
  universal_rules.lower_case @ weekdays @ universal_rules.word_token];

month_range = universal_func.basic_connector[
  range_connector,
  universal_rules.lower_case @ universal_rules.word_token
];

date_range = universal_func.basic_connector[
  range_connector,
  date.DATE_MARKUP
];

# Edit the line 13 of the test file to change kilocubic meter to cubic kilometer
# once the correction has been made to measures grammar.
measure_range = universal_func.connector_multicontext[
  universal_rules.RANGE_CONNECTOR,
  (measure.MEASURE_MARKUP | universal_rules.SIGNED_S_ASIAN_CARDINAL_MARKUP),
  measure.MEASURE_MARKUP
];

telephone_range = universal_func.connector_multicontext[
  universal_rules.RANGE_CONNECTOR,
  (telephone.PHONE_NUMBER_MARKUP),
  (universal_rules.kDigit{2} @ universal_rules.SIGNED_S_ASIAN_CARDINAL_MARKUP)
];

money_range = universal_func.connector_multicontext[
    universal_rules.RANGE_CONNECTOR,
    (universal_rules.MONEY_MARKUP | universal_rules.SIGNED_S_ASIAN_CARDINAL_MARKUP),
    universal_rules.MONEY_MARKUP
];

export CONNECTOR_MARKUP = Optimize[
  (b.BANGLA_NUMERALS_REWRITE*) @
  (date.MONTHS_NAMES_REWRITE*) @
  (date.WEEKDAY_NAMES_REWRITE*) @
  (date.ERA_NAMES_REWRITE*) @
  (universal_rules.CONNECTOR_MARKUP
  | universal_rules.CONSECUTIVE_YEAR_MARKUP
  | universal_rules.YEARS_RANGE
  | universal_rules.BY_MARKUP
  | universal_rules.MULTI_D_MEASURE
  | weekday_range
  | month_range
  | date_range
  | measure_range
  | telephone_range
  | money_range) @
  universal_rules.CLEAN_SPACES
];
