# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Verbalizer for money

import 'number_names_rules.grm' as n;
import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'cardinal.grm' as c;

minus_name = Project[n.MINUS, 'output'];

negative = (universal_rules.negative_true : (minus_name " ")) | universal_rules.negative_false;

major_country_data = StringFile['money_country_major.txt'];
minor_country_data = StringFile['money_country_minor.txt'];
major_data = StringFile['money_major.txt'];
minor_data = StringFile['money_minor.txt'];

major_country_markup = universal_rules.currency major_country_data universal_rules.rec_sep;
minor_country_markup = universal_rules.currency minor_country_data universal_rules.rec_sep;

major_markup = universal_rules.currency major_data universal_rules.rec_sep;
minor_markup = universal_rules.currency minor_data universal_rules.rec_sep;

# Bangla doesn't have singular and plural forms for all currencies except those
# constituting Rupees and Paise
# The plural form is used in money_major.txt, money_minor.txt, NA and NA
# Rewriting the cases when singular form should be used
# E.g. ("একটি টাকা" : "একটি টাকা") (One Rupees : One Rupee)

singular_exception =
 (
   ("একটি টাকা" : "একটি টাকা")
 | ("একটি পয়সা" : "একটি পয়সা")
 | ("একটি ভারতীয় টাকা" : "একটি ভারতীয় টাকা")
 | ("একটি ভারতীয় পয়সা" : "একটি ভারতীয় পয়সা")
 | ("একটি ইন্দোনেশিয়ার টাকা" : "একটি ইন্দোনেশিয়ান টাকা")
 | ("একটি ইন্দোনেশিয়ার পয়সা" : "একটি ইন্দোনেশিয়ার পয়সা")
 | ("একটি শ্রীলঙ্কার টাকা" : "একটি শ্রীলঙ্কান টাকা")
 | ("একটি শ্রীলঙ্কার পয়সা" : "একটি শ্রীলঙ্কার পয়সা")
 | ("একটি মৌরিটিয়ান টাকা" : "একটি মৌরিটিয়ান টাকা")
 | ("একটি মৌরিটিয়ান পয়সা" : "একটি মৌরিটিয়ান পয়সা")
 | ("একটি নেপালি টাকা" : "একটি নেপালি টাকা")
 | ("একটি নেপালি পয়সা" : "একটি নেপালি পয়সা")
 | ("একটি নেপালি টাকা" : "একটি নেপালি টাকা")
 | ("একটি নেপালি পয়সা" : "একটি নেপালি পয়সা")
 | ("একটি পাকিস্তানের টাকা" : "একটি পাকিস্তানি টাকা")
 | ("একটি পাকিস্তানের পয়সা" : "একটি পাকিস্তানের পয়সা")
 | ("একটি সিসিলি টাকা" : "একটি সিসিলি টাকা")
 | ("একটি সিসিলি পয়সা" : "একটি সিসিলি পয়সা")
 )
;
singular_fix = CDRewrite[singular_exception, "", "[EOS]", universal_rules.sigma_star];

#{ money { amount { integer_part: "0" } currency: "usd" style: 1 } }

# Extract the major part
major = (
          negative?
          universal_rules.integer_part
          c.CARDINAL
          universal_rules.rec_sep
        );

# Extract the minor part
minor = (
          universal_rules.fractional_part
          (("0" : "")* c.CARDINAL)
          universal_rules.rec_sep
        );

# To extract major part only and neglect minor part in case it is 1234.00
amount_major_only = (
                      negative?
                      universal_rules.integer_part
                      c.CARDINAL
                      universal_rules.rec_sep
                      (universal_rules.fractional_part ("0" : "")+ universal_rules.rec_sep)?
                    );

# To extract minor part only and neglect major part in case it is 00.1234
amount_minor_only = (
                      negative?
                      (universal_rules.integer_part ("0" : "")+ universal_rules.rec_sep)?
                      universal_rules.fractional_part
                      (universal_rules.non_zero_factorized_number
                       @ (("0" : "")* c.CARDINAL))
                      universal_rules.rec_sep
                    );

# Style 1: 12.50 $ -> বার ডলার পঞ্চাশ সেন্ট (Twelve Dollars Fifty Cents)
style1 = (
           ((major universal_rules.ins_space major_markup) @ singular_fix)
           universal_rules.ins_space
           ((minor universal_rules.ins_space minor_markup) @ singular_fix)
           universal_rules.style1?
         );

# Style 2: 12.50 USD -> বার আমেরিকার ডলার পঞ্চাশ সেন্ট (Twelve United States
# Dollars Fifty United State Cents)
style2 = (
           ((major universal_rules.ins_space major_country_markup) @ singular_fix)
           universal_rules.ins_space
           ((minor universal_rules.ins_space minor_country_markup) @ singular_fix)
           universal_rules.style2
         );

major_only = (
 (((amount_major_only universal_rules.ins_space major_markup)
   @ singular_fix) universal_rules.style1?) |
 (((amount_major_only universal_rules.ins_space major_country_markup)
   @ singular_fix) universal_rules.style2)
)
;
minor_only = (
 (((amount_minor_only universal_rules.ins_space minor_markup)
    @ singular_fix) universal_rules.style1?) |
 (((amount_minor_only universal_rules.ins_space minor_country_markup)
    @ singular_fix) universal_rules.style2)
)
;

export MONEY_MARKUP = Optimize[
  universal_rules.semclass_money
  (style1 | style2 | major_only | minor_only)
];
