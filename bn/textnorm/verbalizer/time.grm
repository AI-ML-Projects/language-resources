# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

#
#Verbalizer for time in bangla

import 'number_names_rules.grm' as n;
import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;

# Time zones from http://en.wikipedia.org/wiki/List_of_time_zone_abbreviations
# Time zones with same acronymns are commented out and such acronymns are
#  verbalized on a letter basis eg : AMT
timezone = StringFile['time_zones.txt'];

hours_morning = "4" | "5" | "6" | "7" | "8" | "9" | "10" | "11";
hours_noon = "12" | ("13" : "1") | ("14" : "2") | ("15" : "3");
hours_evening = ("16" : "4") | ("17" : "5") | ("18" : "6") | ("19" : "7");
hours_night =
  ("20" : "8") | ("21" : "9") | ("22" : "10") | ("23" : "11") |
  ("24" : "12") | ("0" : "12") | "1" | "2" | "3"
;

hours45_morning =
   ("4" : "5") | ("5" : "6") | ("6" : "7") | ("7" : "8") | ("8" : "9")
 | ("9" : "10") | ("10" : "11") | ("11" : "12")
;
hours45_noon = ("12" : "1") | ("13" : "2") | ("14" : "3") | ("15" : "4");
hours45_evening = ("16" : "5") | ("17" : "6") | ("18" : "7") | ("19" : "8");
hours45_night =
  ("20" : "9") | ("21" : "10") | ("22" : "11") | ("23" : "12") |
  ("24" : "1") | ("0" : "1") | ("1" : "2") | ("2" : "3") | ("3" : "4")
;

ins_tta = ("" : "টা");

hours =
  (hours_morning @ (("" : "সকালে ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours_noon @ (("" : "দুপুরে ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours_evening @ (("" : "সন্ধ্যাএ ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours_night @ (("" : "রাতে ") n.CARDINAL_NUMBER_NAME) ins_tta)
;

hours30 =
  (hours_morning @ (("" : "সকালের সাড়ে ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours_noon @ (("" : "দুপুরের সাড়ে ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours_evening @ (("" : "সন্ধ্যার সাড়ে ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours_night @ (("" : "রাত্রের সাড়ে ") n.CARDINAL_NUMBER_NAME) ins_tta)
;

hours45 =
  (hours45_morning @ (("" : "সকালের পৌনে ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours45_noon @ (("" : "দুপুরের পৌনে ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours45_evening @ (("" : "সন্ধ্যার পৌনে ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours45_night @ (("" : "রাত্রের পৌনে ") n.CARDINAL_NUMBER_NAME) ins_tta)
;

hours15 =
  (hours_morning @ (("" : "সকালের সওয়া ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours_noon @ (("" : "দুপুরের সওয়া ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours_evening @ (("" : "সন্ধ্যার সওয়া ") n.CARDINAL_NUMBER_NAME) ins_tta) |
  (hours_night @ (("" : "রাত্রের সওয়া ") n.CARDINAL_NUMBER_NAME) ins_tta)
;

# Exception - 1:30pm
exception1 = (
  universal_rules.hours (("13" : "দুপুর") | ("1" : "রাত")) universal_rules.rec_sep
  universal_rules.minutes ("30" : " এর দেড়টা") universal_rules.rec_sep
);

# Exception - 2:30 pm
exception2 = (
  universal_rules.hours (("14" : "দুপুর") | ("2" : "রাত")) universal_rules.rec_sep
  universal_rules.minutes ("30" : " এর আড়াইটা") universal_rules.rec_sep
);

# exception - half hour : 4:30 - সাড়ে 4
exception3 = (
  universal_rules.hours hours30 universal_rules.rec_sep
  universal_rules.minutes ("30" : "") universal_rules.rec_sep
);

# exception - quarter until : 4:45 - পৌনে 5
exception4 = (
  universal_rules.hours hours45 universal_rules.rec_sep
  universal_rules.minutes ("45" : "") universal_rules.rec_sep
);

# exception - quarter past : 5:15 - সওয়া 5
exception5 = (
  universal_rules.hours hours15 universal_rules.rec_sep
  universal_rules.minutes ("15" : "") universal_rules.rec_sep
);

exception = (
  (exception1<-3>) | (exception2<-3>) | exception3 | exception4 | exception5
);

usual_hrs = (
  universal_rules.hours
  hours
  universal_rules.rec_sep
  (
  ((("" : " সময়") universal_rules.minutes ("0" : ""))<-2>) |
  (("" : " বেজে ") universal_rules.minutes n.CARDINAL_NUMBER_NAME ("" : " মিনিট"))
  )
  universal_rules.rec_sep
);

duration_hours = Optimize[(("0" : "")<-2>)
                           | (("1" : "এক ঘন্টা ")<-2>)
                           | (n.CARDINAL_NUMBER_NAME ("" : " ঘন্টা "))];

duration_minutes = Optimize[
  (("0" : "")<-2>) | n.CARDINAL_NUMBER_NAME("" : " মিনিট ")
];

duration_seconds = Optimize[
  (("0" : "")<-2>) | n.CARDINAL_NUMBER_NAME("" : " সেকেন্ড")
];

# 16:30
# 4:30
# 16:00

style = Optimize[
  ((exception <-30>)
    | usual_hrs
  )
  universal_rules.speak_period_true?
  universal_rules.any_style?
];

# duration

duration = Optimize[
  universal_rules.hours duration_hours universal_rules.rec_sep
  universal_rules.minutes duration_minutes universal_rules.rec_sep
  universal_rules.seconds duration_seconds universal_rules.rec_sep
];

zone_markup = Optimize[
  universal_rules.zone
  universal_rules.ins_space
  timezone
  universal_rules.rec_sep
];

export BD_TIME_MARKUP = Optimize[
  universal_rules.semclass_time
  (style | duration)
  zone_markup?
];


##############################################################################
# India-specific verbalizer.
# The classifier of numbers, time period separation, words for time period are
# different from those in Bangladesh.
# But the translations for duration are the same.

# Append period description and special expression(for 15/30/35 minutes)
# ahead of hour.
func append_period_in[exp] {
  mid_night_hours = ("24" : "12") | ("0" : "12");
  early_morning_hours = "4" | "5";
  morning_hours = "6" | "7" | "8" | "9" | "10" | "11";
  afternoon_hours = "12" | ("13" : "1") | ("14" : "2") | ("15" : "3");
  evening_hours = ("16" : "4") | ("17" : "5");
  late_eve_hours = ("18" : "6") | ("19" : "7");
  night_hours = ("20" : "8") | ("21" : "9") | ("22" : "10") | ("23" : "11") |
    "1" | "2" | "3" ;
  ins_tta = ("" : "টা");

  return ((("" : ("মাঝ রাত " exp)) (mid_night_hours @ n.CARDINAL_NUMBER_NAME)) |
          (("" : ("ভোর " exp)) (early_morning_hours @ n.CARDINAL_NUMBER_NAME)) |
          (("" : ("সকাল " exp)) (morning_hours @ n.CARDINAL_NUMBER_NAME)) |
          (("" : ("দুপুর " exp)) (afternoon_hours @ n.CARDINAL_NUMBER_NAME)) |
          (("" : ("বিকেল " exp)) (evening_hours @ n.CARDINAL_NUMBER_NAME)) |
          (("" : ("সন্ধ্যা " exp)) (late_eve_hours @ n.CARDINAL_NUMBER_NAME)) |
          (("" : ("রাত " exp)) (night_hours @ n.CARDINAL_NUMBER_NAME)))
         ins_tta;
}

hours15_in =
  universal_rules.hours append_period_in["সোয়া "]
  universal_rules.rec_sep universal_rules.minutes ("15" : "") universal_rules.rec_sep
;

hours30_in =
  universal_rules.hours
  (("1" : "রাত দেড়টা") |
   ("13" : "দুপুর দেড়টা") |
   ("2" : "রাত আড়াইটে") |
   ("14" : "দুপুর আড়াইটে") |
   ((universal_rules.util_hours-"1"-"2"-"13"-"14") @ append_period_in["সাড়ে "]))
  universal_rules.rec_sep universal_rules.minutes ("30" : "") universal_rules.rec_sep
;

hours45_in =
  universal_rules.hours append_period_in["পৌনে "]
  universal_rules.rec_sep universal_rules.minutes ("45" : "") universal_rules.rec_sep
;

quarter_time_in = hours15_in | hours30_in | hours45_in;

usual_hrs_in = (
  universal_rules.hours
  append_period_in[""]
  universal_rules.rec_sep
  (
  (universal_rules.minutes ("0" : "")) |
  (("" : " বেজে ") universal_rules.minutes
    ((universal_rules.util_minutes-"0"-"15"-"30"-"45") @ n.CARDINAL_NUMBER_NAME) ("" : " মিনিট"))
  )
  universal_rules.rec_sep
);

style_in = Optimize[
  ((quarter_time_in<-30>) | usual_hrs_in)
  universal_rules.speak_period_true?
  universal_rules.any_style?
];

classifier_change = CDRewrite[
  ("দুইটা" : "দুটো") | ("তিনটা" : "তিনটে") | ("চারটা" : "চারটে") |
  ("ছয়টা" : "ছটা") | ("নয়টা" : "নটা") | ("চৌদ্দটা" : "চোদ্দটা") |
  ("বিশটা" : "কুড়িটা"),
  " " | "[BOS]",
  " " | "[EOS]",
  universal_rules.sigma_star
];

export IN_TIME_MARKUP = Optimize[
  universal_rules.semclass_time
  ((style_in | duration) @ classifier_change)
  zone_markup?
];

export TIME_MARKUP = "";
