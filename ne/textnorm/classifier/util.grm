# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Utility grammar for Nepali. Has some bits for tagging semiotic
# entities as English where appropriate, and consuming that
# again later.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;

# Add code_switch field to identify English numbers.
english_cs = universal_func.InvertMarkup[universal_rules.code_switch]
              universal_rules.ins_space
              universal_rules.ins_quote
              ("" : "__EN")
              universal_rules.ins_quote
              universal_rules.ins_space
;

s = " ";
subfield = "tokens {" s* (universal_rules.kAlpha | "_")+ s* "{" s*;
export ADD_ENGLISH_CS = CDRewrite[english_cs,
                                   subfield,
                                   "",
                                   universal_rules.sigma_star];

export REMOVE_ENGLISH_CS = Optimize[
  (universal_rules.kAlpha | "_")+
  " {"
  universal_rules.code_switch
  universal_rules.quote
  ("__EN" : "")
  universal_rules.quote
  universal_rules.sigma_star
];

# For mapping Hindi factorization to English and mapping the words back again.
# We use this in lieu of a proper Replace[] type thing that could swap out
# part of the FST.
#
# Note that this is *not* meant to imply that these things are equivalent;
# clearly one lakh != one million; we're just simulating the replacement of
# arcs in an existing verbalizer.
factors = ("\[E5\]" : "\[E6\]") | ("\[E7\]" : "\[E9\]") |
          ("\[E9\]" : "\[E12\]") | ("\[E11\]" : "\[E15\]") |
          ("SAsia:" : "WShort:");
export HI_TO_EN_FACTORIZATION = Optimize[
  CDRewrite[factors, "", "", universal_rules.sigma_star]
];

num = ("million" : "lakh") | ("billion" : "crore") |
      ("trillion" : "arab") | ("quadrillion" : "kharab");
export EN_TO_HI_NUMBERS = Optimize[
  CDRewrite[num, " " | "[BOS]", " " | "[EOS]", universal_rules.sigma_star]
];
