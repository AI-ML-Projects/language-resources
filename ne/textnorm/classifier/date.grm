# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Date parser for Nepali

import 'devanagari.grm' as d;
import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;

month_names_mapping = Optimize[
  (("जनवरी" | "वैशाख") : "january") |
  (("फेब्रुअरी" | "जेठ" | "ज्येष्ठ") : "february") |
  (("मार्च" | "असार" | "आषाढ") : "march") |
  (("अप्रिल" | "साउन" | "श्रावण") : "april") |
  (("मे" | "भदौ" | "भाद्र") : "may") |
  (("जुन" | "असोज" | "आश्विन") : "june") |
  (("जुलाइ" | "कात्तिक" | "कार्तिक") : "july") |
  (("अगस्त" | "मङ्सिर" | "मार्ग") : "august") |
  (("सेप्टेम्बर" | "पुस" | "पौष") : "september") |
  (("अक्टुबर" | "माघ") : "october") |
  (("नोभम्बर" | "फागुन" | "फाल्गुन") : "november") |
  (("डिसेम्बर" | "चैत" | "चैत्र") : "december")
];

export MONTH_NAMES = Project[month_names_mapping, 'input'];

export MONTHS_NAMES_REWRITE = CDRewrite[month_names_mapping, "", "",
                                        universal_rules.sigma_star];

weekday_names_mapping = Optimize[
  (("सोमबार" | "सोम") : "monday") |
  (("मङ्गलबार" | "मङ्गल") : "tuesday") |
  (("बुधबार" | "बुध") : "wednesday") |
  (("बिहिबार" | "बिहि") : "thursday") |
  (("शुक्रबार" | "शुक्र") : "friday") |
  (("शनिबार" | "शनि") : "saturday") |
  (("आइतबार" | "आइत") : "sunday")
];

export WEEKDAY_NAMES_REWRITE = CDRewrite[weekday_names_mapping,"","",
                                         universal_rules.sigma_star];

era_names_mapping = Optimize[
  (("ईसा पूर्व" | "ईपू") : "BC") |
  (("ईसा पूर्व" | "ईपू") : "CE") |
  (("ईस्वी संवत्" | "सन्" | "ईस" | "ई.स" "."?) : "AD") |
  (("विक्रम संवत्" | "विसं" | "वि.सं" "."?) : "BS")    # Era in Nepali calendar.
];

export ERA_NAMES_REWRITE = CDRewrite[era_names_mapping,"","",universal_rules.sigma_star];

export DATE_MARKUP = Optimize[(d.DEVANAGARI_NUMERALS_REWRITE*) @
                              (MONTHS_NAMES_REWRITE*) @
                              (WEEKDAY_NAMES_REWRITE*) @
                              (ERA_NAMES_REWRITE*) @
                              universal_rules.DATE_WITH_AMBIGUOUS @
                              universal_rules.DISAMBIGUATE_DMY
];
