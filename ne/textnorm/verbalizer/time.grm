# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Nepali Verbalizer for times. Takes ascii protocol buffers for a
# time and convert it to regular Nepali words.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'cardinal.grm' as c;
import 'lseq.grm' as l;

cardinal = c.CARDINAL;

### Hours definitions.
morning_hours = ("5" | "6" | "7" | "8" | "9" | "10" | "11");
day_time_hours = ("12" | ("13" : "1") | ("14" : "2") | ("15" : "3")
                  | ("16" : "4"));
evening_hours = (("17" : "5") | ("18" : "6"));
night_hours = (("19" : "7") | ("20" : "8") | ("21" : "9") | ("22" : "10")
               | ("23" : "11") | ("0" : "12") | "1" | "2" | "3" | "4");

hours =
  (("" : "बिहान ") (morning_hours @ cardinal)) |
  (("" : "दिउँसो ") (day_time_hours @ cardinal)) |
  (("" : "बेलुका ") (evening_hours @ cardinal)) |
  (("" : "राती ") (night_hours @ cardinal));

verbalize_hours =
  universal_rules.hours
  hours
  ("" : " बजे")
  universal_rules.rec_sep;

verbalize_minutes =
  universal_rules.minutes
  (("" : "र") universal_rules.ins_space (universal_rules.non_zero_number @ cardinal) ("" : " मिनेट")
   | ("0" : ""))
  universal_rules.rec_sep
;

verbalize_seconds =
  universal_rules.seconds
  (universal_rules.ins_space (universal_rules.non_zero_number @ cardinal) ("" : " सेकेन्ड")
   | ("0" : ""))
  universal_rules.rec_sep
;

nepali_12h_time = Optimize[
  verbalize_hours
  verbalize_minutes?
  verbalize_seconds?
  (universal_rules.speak_period_true | universal_rules.speak_period_false)
  universal_rules.style2
];

nepali_24h_time = Optimize[
  universal_rules.hours
  cardinal
  ("" : " बजे")
  universal_rules.rec_sep
  verbalize_minutes?
  verbalize_seconds?
  universal_rules.style1
];

nepali_time = nepali_12h_time | nepali_24h_time;

zone_offset =
    universal_rules.ins_space
    (("+" : "प्लस") | ("-" : "माइनस"))
    universal_rules.ins_space
    ("0" : "")? cardinal
;

zone =
    universal_rules.zone
    (universal_rules.ins_space l.MAPPINGS)*
    zone_offset?
    universal_rules.rec_sep;

export TIME_MARKUP = Optimize[
  universal_rules.semclass_time
  nepali_time
  zone?
];
