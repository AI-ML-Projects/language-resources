# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Nepali Verbalizer for dates. Takes ascii protocol buffers for a
# date and converts it to regular Nepali words.
# Nepali has two ways of verbalizing dates. If the date is Nepali, the month
# name has different verbalization to it's English month name.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'cardinal.grm' as c;

english_months =
  ( "1" : "जनवरी" ) |
  ( "2" : "फेब्रुअरी" ) |
  ( "3" : "मार्च" ) |
  ( "4" : "अप्रिल" ) |
  ( "5" : "मे" ) |
  ( "6" : "जुन" ) |
  ( "7" : "जुलाइ" ) |
  ( "8" : "अगस्त" ) |
  ( "9" : "सेप्टेम्बर" ) |
  ( "10" : "अक्टुबर" ) |
  ( "11" : "नोभम्बर" ) |
  ( "12" : "डिसेम्बर" )
;

nepali_months =
  ( "1" : "वैशाख" ) |
  ( "2" : "जेठ" ) |
  ( "3" : "असार" ) |
  ( "4" : "साउन" ) |
  ( "5" : "भदौ" ) |
  ( "6" : "असोज" ) |
  ( "7" : "कात्तिक" ) |
  ( "8" : "मङ्सिर" ) |
  ( "9" : "पुस" ) |
  ( "10" : "माघ" ) |
  ( "11" : "फागुन" ) |
  ( "12" : "चैत" )
;

english_era_names = Optimize[
  ("BC" : "ईसा पूर्व") |
  ("AD" : "ईस्वी संवत्") |
  ("CE" : "ईसा पूर्व")
];

nepali_era_name = Optimize[
  ("BS" : "विक्रम संवत्")    # Era in Nepali calendar.
];

year_part = universal_rules.year c.CARDINAL universal_rules.rec_sep;
english_month_part = universal_rules.month (universal_rules.delete_initial_zero @ english_months)
  universal_rules.rec_sep;
nepali_month_part = universal_rules.month (universal_rules.delete_initial_zero @ nepali_months)
  universal_rules.rec_sep;
date_part = universal_rules.day c.CARDINAL universal_rules.rec_sep;
english_era_part = universal_rules.era english_era_names universal_rules.rec_sep;
nepali_era_part = universal_rules.era nepali_era_name universal_rules.rec_sep;

# If the date has AD, BC, or BCE that is an English date.
english_style1 = Optimize[
  (year_part universal_rules.ins_space)?
  english_month_part
  universal_rules.ins_space
  date_part
  universal_rules.ins_space
  english_era_part
  universal_rules.style1
];

english_style2 = Optimize[
  date_part
  universal_rules.ins_space
  english_month_part
  (universal_rules.ins_space year_part)?
  universal_rules.ins_space
  english_era_part
  universal_rules.style2
];

# MM-DD-YYYY is not commonly used in Nepali calendar thus it is an English date.
english_style3 = Optimize[
  english_month_part
  universal_rules.ins_space
  date_part
  universal_rules.ins_space
  year_part
  universal_rules.style1
];

# Dates without an era or Nepali era are Nepali dates.
# Day of the Nepali dates will followed by "गते".
nepali_style1 = Optimize[
  (year_part universal_rules.ins_space)?
  nepali_month_part
  universal_rules.ins_space
  (date_part ("" : " गते"))?
  (universal_rules.ins_space nepali_era_part)?
  universal_rules.style1
];

nepali_style2 = Optimize[
  ((date_part ("" : " गते")) universal_rules.ins_space)?
  nepali_month_part
  (universal_rules.ins_space year_part)?
  universal_rules.style1
];

nepali_style3 = Optimize[
  nepali_month_part
  universal_rules.ins_space (date_part ("" : " गते"))
  (universal_rules.ins_space year_part)?
  universal_rules.style2
];

year_only_style = Optimize[
  year_part
  (universal_rules.ins_space
    (nepali_era_part | english_era_part))?
  (universal_rules.style2 | universal_rules.style1)?
];

english_dates = Optimize[
  (english_style1<-3>) |
  english_style2 |
  english_style3
];
nepali_dates = Optimize[
  (nepali_style1<-3>) |
  (nepali_style2<-5>) |
  nepali_style3
];

export DATE_MARKUP = Optimize[
  universal_rules.semclass_date
  (english_dates | (nepali_dates<-5>) | year_only_style)
];
