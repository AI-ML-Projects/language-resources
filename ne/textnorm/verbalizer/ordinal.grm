# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Nepali ordinal verbalization.

import 'devanagari.grm' as d;
import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'cardinal.grm' as c;

# Ordinal numbers are generated by adding ौँ to cardinal numbers, ending in a
# consonant sound or adding औँ to cardinal numbers ending in a vowel sound.
consonant_ending = "ौँ";
vowel_ending = "औँ";

ordinal = c.CARDINAL ("" : "%");
ordinal_consonant_ending = CDRewrite[
  ("%" : consonant_ending),
  d.C, "[EOS]", universal_rules.sigma_star
];

ordinal_vowel_ending = CDRewrite[
  ("%" : vowel_ending),
  (d.V | d.VD), "[EOS]", universal_rules.sigma_star
];

# Number 1, 2, 3, 4, 6, and 9 doesn't follow the general rule.
correct_exceptions = CDRewrite[
  ("एकौँ" : "प्रथम") |    # 1
  ("दुईऔँ" : "द्वितीय") |    # 2
  ("तिनौँ" : "तृतीय") |    # 3
  ("चारौँ" : "चौथो") |    # 4
  ("छौँ" : "छैटौँ") |    # 6
  ("नौऔँ" : "नवौँ"),    # 9
  "[BOS]", " " | "[EOS]", universal_rules.sigma_star
];

export ORDINAL = Optimize[
  ordinal @
  ordinal_consonant_ending @
  ordinal_vowel_ending @
  correct_exceptions
];

export ORDINAL_MARKUP = Optimize[
  universal_rules.semclass_ordinal
  universal_rules.integer
  ORDINAL
  universal_rules.rec_sep
];
