# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Javanese verbalizer for fractions.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'cardinal.grm' as c;

# 1/2, 1/3, 1/4, ..., 1/9.
# Equivalent to half, third and quarter... Only when the numerator
# is one.
sepra =
   ("" : "pra")
   (("1" | "3" | "5" | "6" | "7" | "8" | "9") @ c.CARDINAL)
;
with_special_denominators =
  (("1" : "se")
  universal_rules.rec_sep
  universal_rules.denominator
  # 1/2 >> separo. 1/4 >> seprapat.
  (("2" : "paro") | ("4" : "prapat") | sepra)) |
  (("3" : "telung")
  universal_rules.rec_sep
  universal_rules.denominator
  # 3/4 >> telungprapat.
  universal_rules.ins_space
  ("4" : "prapat"))
;

# General case:Â two cardinals separated by "pra".
# 2/3 >> loro pra telu.
with_normal_denominator =
  c.CARDINAL
  universal_rules.rec_sep
  universal_rules.denominator
  ("" : " pra ")
  c.CARDINAL
;

# e.g. 5/6 >> lima pra enem.
style_simple = Optimize[
  universal_rules.numerator
  ("-" : (c.MINUS_NAME universal_rules.ins_space))?
  (with_normal_denominator | (with_special_denominators <-5>))
  universal_rules.rec_sep
];

# e.g. 3 1/5.
style_composite = Optimize[
  universal_rules.integer_part
  c.CARDINAL universal_rules.ins_space
  universal_rules.rec_sep
  universal_rules.numerator
  ("-" : (c.MINUS_NAME universal_rules.ins_space))?
  with_normal_denominator
  universal_rules.rec_sep
];

export FRACTION = Optimize[
  style_simple | style_composite
];

export FRACTION_MARKUP = Optimize[
  universal_rules.semclass_fraction FRACTION
];

export FRACTION_MARKUP_SIMPLE = Optimize[
  universal_rules.semclass_fraction
  style_simple
];
