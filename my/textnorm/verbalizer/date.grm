# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Burmese Verbalizer for dates. Takes ascii protocol buffers for a
# date and convert it to regular Burmese words.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'cardinal.grm' as c;

month_number_to_full_name =
    ("1" : "ဇန်နဝါရီ")
  | ("2" : "ဖေဖော်ဝါရီ")
  | ("3" : "မတ်")
  | ("4" : "ဧပြီ")
  | ("5" : "မေ")
  | ("6" : "ဇွန်")
  | ("7" : "ဇူလိုင်")
  | ("8" : "ဩဂုတ်")
  | ("9" : "စက်တင်ဘာ")
  | ("10" : "အောက်တိုဘာ")
  | ("11" : "နိုဝင်ဘာ")
  | ("12" : "ဒီဇင်ဘာ")
;

month_full_name =
    "ဇန်နဝါရီ"
  | "ဖေဖော်ဝါရီ"
  | "မတ်"
  | "ဧပြီ"
  | "မေ"
  | "ဇွန်"
  | "ဇူလိုင်"
  | "ဩဂုတ်"
  | "စက်တင်ဘာ"
  | "အောက်တိုဘာ"
  | "နိုဝင်ဘာ"
  | "ဒီဇင်ဘာ"
;

month_ordinal_name =
    "တစ်_unit လပိုင်း"
  | "နှစ်_unit လပိုင်း"
  | "သုံး လပိုင်း"
  | "လေး လပိုင်း"
  | "ငါး လပိုင်း"
  | "ခြောက် လပိုင်း"
  | "ခုနှစ် လပိုင်း"
  | "ရှစ် လပိုင်း"
  | "ကိုး လပိုင်း"
  | "ဆယ် လပိုင်း"
  | "ဆယ့် တစ်_unit လပိုင်း"
  | "ဆယ့် နှစ်_unit လပိုင်း"
;

months = month_number_to_full_name | month_full_name | month_ordinal_name;

weekdays =
    "တနင်္လာ"
  | "အင်္ဂါ"
  | "ဗုဒ္ဓဟူး"
  | "ကြာသပတေး"
  | "သောကြာ"
  | "စနေ"
  | "တနင်္ဂနွေ"
;

eras = "အေဒီ" | "ဘီစီ";

day_classifier = ("" : "ရက်") | ("ရက်နေ့" <-0.5>);
weekday_classifier = "" : "နေ့";
month_classifier = "" : "လ";
year_classifier = "" : "ခုနှစ်";

weekday_markup = Optimize[
  universal_rules.weekday
  weekdays
  universal_rules.ins_space
  weekday_classifier
  universal_rules.rec_sep
];

day_markup = Optimize[
  universal_rules.day c.CARDINAL_PRECEDING_UNIT universal_rules.ins_space day_classifier universal_rules.rec_sep
  (universal_rules.ins_space weekday_markup)?
];

month_markup = Optimize[
  universal_rules.month
  (universal_rules.delete_initial_zero @ months)
  universal_rules.ins_space
  month_classifier
  universal_rules.rec_sep
];

era_markup =  Optimize[universal_rules.era eras universal_rules.rec_sep];
year_markup = Optimize[
  (era_markup universal_rules.ins_space)?
  universal_rules.year c.CARDINAL_PRECEDING_UNIT universal_rules.ins_space year_classifier universal_rules.rec_sep
];

# Burmese dates have complex format where dates and year can appear both before
# and after the month and may also be skipped. See the classifier for examples.
style1 = Optimize[
    (year_markup universal_rules.ins_space)?
    (day_markup universal_rules.ins_space)?
    month_markup
    (universal_rules.ins_space day_markup)?
    (universal_rules.ins_space year_markup)?
    universal_rules.style1
];

# Style2 applies to format "day month" with or without weekday,
# where day must be verbalized before month.
style2 = Optimize[
    universal_rules.day c.CARDINAL_PRECEDING_UNIT universal_rules.rec_sep
    universal_rules.ins_space
    month_markup
    (universal_rules.ins_space weekday_markup)?
    universal_rules.style2
];

export DATE_MARKUP = Optimize[
  universal_rules.semclass_date
  (style1 | style2)
];
