# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Parser for Khmer letter sequences "អនក្រ", "ត.ទ.ស្រ." etc.

import 'khmer.grm' as km;
import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;

lseq_whitelist = Optimize[StringFile['lseq_whitelist.tsv']];

cluster = km.KHMER_ALPHABET km.KHMER_COENG km.KHMER_ALPHABET;
consonant_or_cluster = km.KHMER_ALPHABET | cluster;

export LSEQ =
  (lseq_whitelist @ universal_rules.LSEQ_MARKUP) |
  universal_rules.LSEQ;

# Initials with fullstops in between; ត.ទ.ស្រ. should become
# letters "ត.ទ.ស្រ." for example, not three separate tokens.
letters_fullstop =
  (universal_rules.kUpper | consonant_or_cluster){1,3}
  universal_rules.clear_space? "." universal_rules.clear_space?
;

export LSEQ_FULLSTOPS = Optimize[
  (
      (letters_fullstop letters_fullstop+)
    | (km.KHMER_ALPHABET{2,4} "." universal_rules.clear_space?)
  )
  @ universal_rules.LSEQ_MARKUP
];
